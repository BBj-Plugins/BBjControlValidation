use ::BBjControlValidation/Validator.bbj::Validator
rem /**
rem  * Validates that the value is divisible by the given number.
rem  *
rem  * @author Hyyan Abo Fakher
rem  */
class public DivisibleBy extends Validator
  rem /**
  rem  * @param comparedValue! - the value to compare with
  rem  */
  method public DivisibleBy(BBjNumber comparedValue!)
    #this!(comparedValue!, "This value should be a multiple of ""{{ comparedValue }}"".")
  methodend
  rem /**
  rem  * @param comparedValue! - the value to compare with
  rem  */
  method public DivisibleBy(BBjNumber comparedValue!, BBjString message$)
    #super!(message$, "")
    #Validator$ = "" +
:    "const comparedValue = " + str(comparedValue!) + ";" +
:    "if (String(x).trim().length && 0 !== Number(x) % comparedValue) {" +
:    "  addViolation('" + message$ + "', {comparedValue});" +
:    "}"
  methodend

classend
